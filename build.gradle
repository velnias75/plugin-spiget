/*
 * Add to your build.gradle-dependencies:
 *
 * implementation(project(path: ':spiget', configuration: 'shadow')) {
 *		exclude group:'dev.derklaro.spiget'
 *	}
 *	
 *	shadow(project(':spiget'))
 */

plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

apply plugin: 'java-library'

archivesBaseName = 'spiget'

repositories {
	mavenLocal()
	mavenCentral()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

dependencies {

	implementation("dev.derklaro.spiget:http-java8:${project.spiget_version}")
	shadow("dev.derklaro.spiget:http-java8:${project.spiget_version}") 
	
	implementation("dev.derklaro.spiget:mapper-gson:${project.spiget_version}")
	shadow("dev.derklaro.spiget:mapper-gson:${project.spiget_version}")
	
	compileOnly("com.google.guava:guava:${project.guava_version}")
	
	testImplementation("dev.derklaro.spiget:http-java8:${project.spiget_version}")
	testImplementation("dev.derklaro.spiget:mapper-gson:${project.spiget_version}")
	
	testImplementation("org.junit.jupiter:junit-jupiter-api:${project.junit_version}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${project.junit_version}")
    
    testRuntimeOnly("com.google.code.gson:gson:${project.gson_version}")
    testRuntimeOnly("com.google.guava:guava:${project.guava_version}")

}

shadowJar {

	archiveClassifier.set('')
    
    relocate 'dev.derklaro.spiget', 'de.rangun.spiget.shadowed.dev.derklaro.spiget'

    exclude 'META-INF/**'
    exclude 'license.txt'

    minimize()
}

assemble {
	dependsOn shadowJar
}

test {
	
	mustRunAfter(jar)
	dependsOn(shadowJar)
	
	classpath = project.sourceSets.test.output + configurations.testRuntimeClasspath + files(shadowJar.archiveFile)
	
	useJUnitPlatform()
}
